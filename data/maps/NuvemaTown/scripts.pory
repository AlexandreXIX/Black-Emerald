const LOCALID_JUNIPER = 1
const LOCALID_CHEREN = 2
const LOCALID_BIANCA = 3
const LOCALID_PIDOVE1 = 4
const LOCALID_PIDOVE2 = 5
const LOCALID_PIDOVE3 = 6
const LOCALID_PIDOVE4 = 7

// State descriptions for VAR_NUVEMA_TOWN_STATE
// 1: Saw exit house cutscene
// 2: Talked to Bianca's dad
// 3: Got Pok√©Dex

// State descriptions for VAR_NUVEMA_INTRO_STATE
// 0: End of new game sequence
// 1: Juniper Walked
// 2: Meet the Rivals
// 3: Picked Starter
// 4: Finished battles
// 5: Finished meeting mom

// OLD State descriptions for VAR_RIVAL_BIANCA_STATE
//                    and VAR_RIVAL_CHEREN_STATE
// 1: On Truck (and is corresponding gender)
// 2: Met Rival's Mom (and is corresponding gender)
// 3: Entered Hall of Fame
// 4: Completed Lati TV / SS Ticket event
// 5: Exited house after above event

// Old Var unusued: VAR_NUVEMA_RIVAL_STATE
// State descriptions for VAR_NUVEMA_RIVAL_STATE
// 1: Finished initial rival battle
// 2: Apologized to mom
// 3: Bianca talked to her dad

mapscripts NuvemaTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: NuvemaTown_OnTransition
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_NUVEMA_INTRO_STATE, 0: NuvemaTown_EventScript_JuniperIntro
		VAR_NUVEMA_TOWN_STATE, 0: NuvemaTown_EventScript_PidoveCutscene
	]
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [VAR_NUVEMA_INTRO_STATE, 0: NuvemaTown_OnWarp_Intro
									   VAR_NUVEMA_TOWN_STATE, 0: NuvemaTown_OnWarp_FirstExit]
}

script NuvemaTown_OnTransition {
	setflag(FLAG_VISITED_NUVEMA_TOWN)
	end
}

script NuvemaTown_OnWarp_Intro {
	applymovement(OBJ_EVENT_ID_PLAYER, moves(set_invisible))
	applymovement(LOCALID_CHEREN, moves(set_invisible))
	applymovement(LOCALID_BIANCA, moves(set_invisible))
	setflag(FLAG_HIDE_NUVEMA_TOWN_PIDOVES)
}

script NuvemaTown_OnWarp_FirstExit {
	setobjectxy(LOCALID_BIANCA, 17, 15)
}

script NuvemaTown_EventScript_JuniperIntro {
    lockall
    special(SpawnCameraObject)
	playbgm(MUS_FOLLOW_ME, TRUE)
	applymovement(LOCALID_JUNIPER, NuvemaTown_LabToHouse)
	applymovement(OBJ_EVENT_ID_CAMERA, NuvemaTown_LabToHouse)
	applymovement(LOCALID_PIDOVE1, moves(set_invisible))
	applymovement(LOCALID_PIDOVE2, moves(set_invisible))
	applymovement(LOCALID_PIDOVE3, moves(set_invisible))
	applymovement(LOCALID_PIDOVE4, moves(set_invisible))
	waitmovement(OBJ_EVENT_ID_CAMERA)
	opendoor(18,12)
	waitdooranim
	applymovement(LOCALID_JUNIPER, NuvemaTown_EnterHouse)
	applymovement(OBJ_EVENT_ID_CAMERA, NuvemaTown_EnterHouse)
	waitmovement(0)
	setvar(VAR_NUVEMA_INTRO_STATE, 1)
	setflag(FLAG_HIDE_NUVEMA_TOWN_INTRO_JUNIPER)
	clearflag(FLAG_HIDE_NUVEMA_TOWN_PIDOVES)
	clearflag(FLAG_HIDE_MAP_NAME_POPUP)
	hideobjectat(LOCALID_JUNIPER, MAP_NUVEMA_TOWN)
	closedoor(18,12)
	waitdooranim
	delay(30)
	opendoor(18,12)
	waitdooranim
	showobjectat(LOCALID_JUNIPER, MAP_NUVEMA_TOWN)
	applymovement(LOCALID_JUNIPER, NuvemaTown_ExitHouse)
	waitmovement(0)
	closedoor(18,12)
	waitdooranim
	applymovement(LOCALID_JUNIPER, NuvemaTown_WalkOffCamera)
	waitmovement(0)
	removeobject(LOCALID_JUNIPER)
	warpsilent(MAP_NUVEMA_TOWN_PLAYERS_HOUSE_2F, 4, 6)
	waitstate
    releaseall
}

script NuvemaTown_EventScript_PidoveCutscene {
	lockall
	applymovement(LOCALID_BIANCA, moves(walk_down * 5))
	applymovement(LOCALID_PIDOVE1, moves(walk_left * 10))
	playse(SE_M_RAZOR_WIND)
	applymovement(LOCALID_PIDOVE2, moves(walk_diag_southeast * 10))
	playse(SE_M_RAZOR_WIND)
	applymovement(LOCALID_PIDOVE3, moves(walk_diag_southeast * 10))
	playse(SE_M_RAZOR_WIND)
	applymovement(LOCALID_PIDOVE4, moves(walk_diag_northwest * 10))
	playse(SE_M_RAZOR_WIND)
	waitmovement(LOCALID_BIANCA)
	removeobject(LOCALID_BIANCA)
	removeobject(LOCALID_PIDOVE1)
	removeobject(LOCALID_PIDOVE2)
	removeobject(LOCALID_PIDOVE3)
	removeobject(LOCALID_PIDOVE4)
	setvar(VAR_NUVEMA_TOWN_STATE, 1)
	setflag(FLAG_HIDE_NUVEMA_TOWN_PIDOVES)
	releaseall
	end
}

movement NuvemaTown_LabToHouse {
    walk_down * 8,
    walk_right * 5,
    face_up * 1
}

movement NuvemaTown_EnterHouse {
    walk_up * 1
}

movement NuvemaTown_ExitHouse {
    walk_down * 1
}

movement NuvemaTown_WalkOffCamera {
    walk_down * 6
}