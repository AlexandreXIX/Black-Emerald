raw `
.set LOCALID_STARTERS, 1
.set LOCALID_CHEREN, 2
.set LOCALID_BIANCA, 3

NuvemaTown_PlayersHouse_2F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, NuvemaTown_PlayersHouse_2F_OnTransition
	map_script MAP_SCRIPT_ON_FRAME_TABLE, NuvemaTown_PlayersHouse_2F_OnFrame
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, NuvemaTown_PlayersHouse_2F_OnWarp
	.byte 0

NuvemaTown_PlayersHouse_2F_OnTransition:
	call SecretBase_EventScript_SetDecorationFlags
	setvar VAR_SECRET_BASE_INITIALIZED, 0
	end

NuvemaTown_PlayersHouse_2F_OnFrame:
	map_script_2 VAR_NUVEMA_INTRO_STATE, 1, NuvemaTown_PlayersHouse_2F_EventScript_IntroCutscene
	.2byte 0

NuvemaTown_PlayersHouse_2F_OnWarp:
	map_script_2 VAR_SECRET_BASE_INITIALIZED, 0, NuvemaTown_PlayersHouse_2F_EventScript_CheckInitDecor
	.2byte 0

NuvemaTown_PlayersHouse_2F_EventScript_CheckInitDecor::
	checkplayergender
	goto_if_eq VAR_RESULT, MALE, SecretBase_EventScript_InitDecorations
	end

NuvemaTown_PlayersHouse_2F_EventScript_CheckPlayersPC::
	setvar VAR_0x8004, PC_LOCATION_PLAYERS_HOUSE
	special DoPCTurnOnEffect
	playse SE_PC_ON
	msgbox gText_PlayerHouseBootPC, MSGBOX_DEFAULT
	special BedroomPC
	waitstate
	releaseall
	end

NuvemaTown_PlayersHouse_2F_EventScript_TurnOffPlayerPC::
	setvar VAR_0x8004, PC_LOCATION_PLAYERS_HOUSE
	playse SE_PC_OFF
	special DoPCTurnOffEffect
	releaseall
	end

PlayersHouse_2F_EventScript_Notebook::
	msgbox PlayersHouse_2F_Text_Notebook, MSGBOX_SIGN
	end

PlayersHouse_2F_EventScript_GameCube::
	msgbox PlayersHouse_2F_Text_ItsAGameCube, MSGBOX_SIGN
	end

PlayersHouse_2F_Text_Notebook:
	.string "{PLAYER} flipped open the notebook.\p"
	.string "ADVENTURE RULE NO. 1\n"
	.string "Open the MENU with START.\p"
	.string "ADVENTURE RULE NO. 2\n"
	.string "Record your progress with SAVE.\p"
	.string "The remaining pages are blank…$"

Common_Text_LookCloserAtMap:
	.string "{PLAYER} took a closer look at the\n"
	.string "UNOVA region map.$"

PlayersHouse_2F_Text_ItsAGameCube:
	.string "It's a Nintendo GameCube.\p"
	.string "A Game Boy Advance is connected to\n"
	.string "serve as the Controller.$"
`

// List of flags and variables to later check:
// VAR_NUVEMA_RIVAL_STATE
// FLAG_MET_RIVAL_LILYCOVE
// VAR_BIRCH_LAB_STATE
// VAR_NUVEMA_TOWN_STATE
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_HILBERT
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_2F_POKE_BALL
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_RIVAL_BEDROOM
// VAR_NUVEMA_HOUSES_STATE_MAY
// VAR_NUVEMA_HOUSES_STATE_HILBERT
// FLAG_RESCUED_BIRCH
// VAR_NUVEMA_INTRO_STATE
// FLAG_RECEIVED_RUNNING_SHOES
// FLAG_MET_RIVAL_MOM
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_VIGOROTH_1
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_VIGOROTH_2
// VAR_TEMP_1
// FLAG_SYS_TV_HOME
// FLAG_HAS_MATCH_CALL
// FLAG_ENABLE_MOM_MATCH_CALL
// FLAG_RECEIVED_AMULET_COIN
// GAME_STAT_RESTED_AT_HOME
// FLAG_RECEIVED_SS_TICKET
// FLAG_SYS_TV_LATIAS_LATIOS
// FLAG_LATIOS_OR_LATIAS_ROAMING
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_MOM

// removed running shoes manual as metatile

// For future use:
//NuvemaTown_PlayersHouse_1F_EventScript_SetMovingBoxes::
//	setmetatile 5, 4, METATILE_PlayersHouse_MovingBox_Open, TRUE
//	setmetatile 5, 2, METATILE_PlayersHouse_MovingBox_Closed, TRUE
//	return


// Common_EventScript_SetupRivalGfxId


// special TurnOnTVScreen
// special TurnOffTVScreen
// special InitRoamer


// Useful for later
// EventScript_PlayersHouseLatiNewsFlash::
// 	msgbox PlayersHouse_1F_Text_LatiEmergencyNewsFlash, MSGBOX_DEFAULT
// 	special InitRoamer
// 	clearflag FLAG_SYS_TV_LATIAS_LATIOS
// 	setflag FLAG_LATIOS_OR_LATIAS_ROAMING
// 	special TurnOffTVScreen
// 	releaseall
// 	end

script NuvemaTown_PlayersHouse_2F_EventScript_IntroCutscene {
	lockall
	applymovement(LOCALID_CHEREN, moves(walk_down, face_left))
	waitmovement(0)
    msgbox(format("Cheren: {PLAYER}!\pI heard from Professor Juniper. We can have a Pokémon?"))
	applymovement(LOCALID_CHEREN, moves(face_down))
	waitmovement(0)
	msgbox(format("...\n...\pWhat's keeping Bianca?"))
	applymovement(LOCALID_CHEREN, moves(face_left))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
	waitmovement(0)
	msgbox(format("What time is it anyways?"))
	call(PlayersHouse_2F_EventScript_SetWallClock)
    addobject(LOCALID_BIANCA)
	applymovement(LOCALID_BIANCA, moves(walk_down))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
	waitmovement(0)
	applymovement(LOCALID_CHEREN, moves(face_up))
	waitmovement(0)
    msgbox(format("Bianca: Am I a little late again? So-oooo-orry!"))
	applymovement(LOCALID_CHEREN, moves(walk_right, walk_up))
	waitmovement(0)
    msgbox(format("Cheren: Bianca...\pI've known for ten years that you have no sense of time, but...seriously...\pToday's the day we can get a Pokémon from Professor Juniper!"))
	applymovement(LOCALID_BIANCA, moves(walk_down, walk_left * 4, walk_down))
	waitmovement(0)
	applymovement(LOCALID_CHEREN, moves(face_left))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
	waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("Bianca: I know. Sorry, {PLAYER}.\nSorry, Cheren."))
	applymovement(LOCALID_BIANCA, moves(walk_left, walk_down * 2, face_player))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
	waitmovement(0)
    msgbox(format("Bianca: So, where are the Pokémon?\pThey were delivered to {PLAYER}'s house, so {PLAYER} gets first pick."))
	msgbox(format("Cheren: Naturally."))
	applymovement(LOCALID_CHEREN, moves(walk_down, walk_left, face_down))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
	waitmovement(0)
	applymovement(LOCALID_BIANCA, moves(face_down))
	waitmovement(0)
    msgbox(format("The Pokémon are waiting for us inside that gift box.\pOk, {PLAYER}, you go first and take a peek at the gift box.\pI want to meet the Pokémon right now!"))
	setvar(VAR_NUVEMA_INTRO_STATE, 2)
	releaseall
}

script NuvemaTown_PlayersHouse_2F_EventScript_StarterBag {
	lockall
	msgbox(format("{PLAYER} opened the gift box!"))
	setflag(FLAG_SYS_POKEMON_GET)
	fadescreen(FADE_TO_BLACK)
	special(ChooseStarter)
	setflag(FLAG_SYS_POKEDEX_GET) //TMP
	special(SetUnlockedPokedexFlags) //TMP
	setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH) //TMP
	waitstate
	playfanfare(MUS_OBTAIN_ITEM)
	switch (var(VAR_STARTER_MON)) {
		case 0:
			msgbox(format("{PLAYER} chose \n Snivy!"))
		case 1:
			msgbox(format("{PLAYER} chose \n Tepig!"))
		case 2:
			msgbox(format("{PLAYER} chose \n Oshawott!"))
		default:
			msgbox("Error getting starter!")
	}
	waitfanfare
	setvar(VAR_NUVEMA_INTRO_STATE, 3)
	goto(NuvemaTown_PlayersHouse_2F_Rivals_Pick_Starter)
	releaseall
}

script NuvemaTown_PlayersHouse_2F_Rivals_Pick_Starter {
	lockall
	applymovement(LOCALID_BIANCA, moves(walk_down, face_right))
	waitmovement(0)
	msgbox(format("Bianca: OK, I'll take this Pokémon!\NCheren, that one's yours!"))
	applymovement(LOCALID_CHEREN, moves(walk_down, face_left))
	waitmovement(0)
	msgbox(format("Cheren: Hey, how come you get to pick out my Pokémon?"))
	switch(var(VAR_STARTER_MON)) {
		case 0:
			msgbox(format("Oh, never mind. I wanted Tepig from the start, anyway."))
		case 1:
			msgbox(format("Oh, never mind. I wanted Oshawott from the start, anyway."))
		case 2:
			msgbox(format("Oh, never mind. I wanted Snivy from the start, anyway."))
		default:
			msgbox("Error getting starter!")
	}
	fadescreen(FADE_TO_BLACK)
	setobjectxy(OBJ_EVENT_ID_PLAYER, 3, 6)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
	special(SpawnCameraObject)
	setobjectxy(OBJ_EVENT_ID_CAMERA, 4, 5)
	setobjectxy(LOCALID_BIANCA, 5, 7)
	applymovement(LOCALID_BIANCA, moves(face_left))
	waitmovement(0)
	setobjectxy(LOCALID_CHEREN, 4, 4)
	applymovement(LOCALID_CHEREN, moves(face_down))
	waitmovement(0)
	goto(NuvemaTown_PlayersHouse_2F_Rival_Battles)
	releaseall
}

script NuvemaTown_PlayersHouse_2F_Rival_Battles {
	lockall
	fadescreen(FADE_FROM_BLACK)
	applymovement(LOCALID_BIANCA, moves(walk_up, face_left))
	waitmovement(0)
	msgbox(format("Bianca: Everyone has chosen a Pokémon!\N...So that's that!"))
	msgbox(format("Hey, I know!\nLet's have a Pokémon battle!"))
	msgbox(format("Cheren: Honestly, Bianca..."))
	msgbox(format("Even though they're still weak Pokémon, you shouldn't have Pokémon battles inside a house."))
	msgbox(format("Bianca: Don't be a worrywart!\nThese little ones are weak, like you said."))
	msgbox(format("We have to let them battle so they'll get stronger. It's settled, {PLAYER}!\NGet ready for a Pokémon battle!"))
	switch(var(VAR_STARTER_MON)) {
		// Dont know about midbattle text
		case 0: trainerbattle_no_intro(TRAINER_BIANCA_INTRO_SNIVY, NuvemaTown_PlayersHouse_2F_Bianca_Lost)
		case 1: trainerbattle_no_intro(TRAINER_BIANCA_INTRO_TEPIG, NuvemaTown_PlayersHouse_2F_Bianca_Lost)
		case 2: trainerbattle_no_intro(TRAINER_BIANCA_INTRO_OSHAWOTT, NuvemaTown_PlayersHouse_2F_Bianca_Lost)
	}
	// Room gets destroyed
	msgbox(format("Bianca: Wow...\N{PLAYER}, you're gonna be an awesome Trainer someday, I can tell!\pNo doubt!"))
	msgbox(format("Cheren: Uh...\n...\pBianca, would you take a look around?"))
	applymovement(LOCALID_BIANCA, moves(face_right))
	waitmovement(0)
	delay(60)
	applymovement(LOCALID_BIANCA, moves(face_up))
	waitmovement(0)
	delay(60)
	applymovement(LOCALID_BIANCA, moves(face_down))
	waitmovement(0)
	delay(60)
		msgbox(format("Bianca: Wh-whoa!\lWhat ahppened?!"))
	applymovement(LOCALID_BIANCA, moves(face_left))
	waitmovement(0)
	msgbox(format("Wow! Pokémon are amaaaaazing!! So little, but so strong!\pI'm so glad that I get to have a Pokémon!"))
	msgbox(format("...Oh.\nUm, sorry about your room, {PLAYER}."))
	applymovement(LOCALID_CHEREN, moves(walk_down * 2, face_right))
	waitmovement(0)
	msgbox(format("Cheren: You...are completely hopeless.\pHere!\lI'll restore your Pokémon for you."))
	playfanfare(SE_USE_ITEM)
	msgbox(format("Cheren healed Bianca's Pokémon!"))
	waitfanfare
	applymovement(LOCALID_CHEREN, moves(face_left))
	waitmovement(0)
	msgbox(format("{PLAYER}'s Pokémon needs to be fixed up, too."))
	playfanfare(SE_USE_ITEM)
	msgbox(format("Cheren healed {PLAYER}'s Pokémon!"))
	waitfanfare
	releaseall
}

script PlayersHouse_2F_EventScript_CheckWallClock {
	lockall
	incrementgamestat(GAME_STAT_CHECKED_CLOCK)
	fadescreen(FADE_TO_BLACK)
	special(Special_ViewWallClock)
	waitstate
	releaseall
	end
}

script PlayersHouse_2F_EventScript_SetWallClock {
	lockall
	fadescreen(FADE_TO_BLACK)
	special(StartWallClock)
	waitstate
	setflag(FLAG_SET_WALL_CLOCK)
	releaseall
	return
}

text NuvemaTown_PlayersHouse_2F_Bianca_Lost{
    format("The Pokémon on both sides did their best!")
}
