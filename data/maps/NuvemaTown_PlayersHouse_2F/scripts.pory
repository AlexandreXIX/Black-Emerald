raw `
.set LOCALID_STARTERS, 1
.set LOCALID_CHEREN, 2
.set LOCALID_BIANCA, 3

NuvemaTown_PlayersHouse_2F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, NuvemaTown_PlayersHouse_2F_OnTransition
	map_script MAP_SCRIPT_ON_FRAME_TABLE, NuvemaTown_PlayersHouse_2F_OnFrame
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, NuvemaTown_PlayersHouse_2F_OnWarp
	.byte 0

NuvemaTown_PlayersHouse_2F_OnTransition:
	call SecretBase_EventScript_SetDecorationFlags
	setvar VAR_SECRET_BASE_INITIALIZED, 0
	end

NuvemaTown_PlayersHouse_2F_OnFrame:
	map_script_2 VAR_NUVEMA_TOWN_STATE, 1, NuvemaTown_PlayersHouse_2F_EventScript_IntroCutscene
	.2byte 0

NuvemaTown_PlayersHouse_2F_OnWarp:
	map_script_2 VAR_SECRET_BASE_INITIALIZED, 0, NuvemaTown_PlayersHouse_2F_EventScript_CheckInitDecor
	.2byte 0

NuvemaTown_PlayersHouse_2F_EventScript_CheckInitDecor::
	checkplayergender
	goto_if_eq VAR_RESULT, MALE, SecretBase_EventScript_InitDecorations
	end

NuvemaTown_PlayersHouse_2F_EventScript_CheckPlayersPC::
	setvar VAR_0x8004, PC_LOCATION_PLAYERS_HOUSE
	special DoPCTurnOnEffect
	playse SE_PC_ON
	msgbox gText_PlayerHouseBootPC, MSGBOX_DEFAULT
	special BedroomPC
	waitstate
	releaseall
	end

NuvemaTown_PlayersHouse_2F_EventScript_TurnOffPlayerPC::
	setvar VAR_0x8004, PC_LOCATION_PLAYERS_HOUSE
	playse SE_PC_OFF
	special DoPCTurnOffEffect
	releaseall
	end

PlayersHouse_2F_EventScript_Notebook::
	msgbox PlayersHouse_2F_Text_Notebook, MSGBOX_SIGN
	end

PlayersHouse_2F_EventScript_GameCube::
	msgbox PlayersHouse_2F_Text_ItsAGameCube, MSGBOX_SIGN
	end

PlayersHouse_2F_Text_Notebook:
	.string "{PLAYER} flipped open the notebook.\p"
	.string "ADVENTURE RULE NO. 1\n"
	.string "Open the MENU with START.\p"
	.string "ADVENTURE RULE NO. 2\n"
	.string "Record your progress with SAVE.\p"
	.string "The remaining pages are blank…$"

Common_Text_LookCloserAtMap:
	.string "{PLAYER} took a closer look at the\n"
	.string "UNOVA region map.$"

PlayersHouse_2F_Text_ItsAGameCube:
	.string "It's a Nintendo GameCube.\p"
	.string "A Game Boy Advance is connected to\n"
	.string "serve as the Controller.$"
`

// List of flags and variables to later check:
// VAR_NUVEMA_RIVAL_STATE
// FLAG_MET_RIVAL_LILYCOVE
// VAR_BIRCH_LAB_STATE
// VAR_NUVEMA_TOWN_STATE
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_HILBERT
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_2F_POKE_BALL
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_RIVAL_BEDROOM
// VAR_NUVEMA_HOUSES_STATE_MAY
// VAR_NUVEMA_HOUSES_STATE_HILBERT
// FLAG_RESCUED_BIRCH
// VAR_NUVEMA_INTRO_STATE
// FLAG_RECEIVED_RUNNING_SHOES
// FLAG_MET_RIVAL_MOM
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_VIGOROTH_1
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_VIGOROTH_2
// VAR_TEMP_1
// FLAG_SYS_TV_HOME
// FLAG_HAS_MATCH_CALL
// FLAG_ENABLE_MOM_MATCH_CALL
// FLAG_RECEIVED_AMULET_COIN
// GAME_STAT_RESTED_AT_HOME
// FLAG_RECEIVED_SS_TICKET
// FLAG_SYS_TV_LATIAS_LATIOS
// FLAG_LATIOS_OR_LATIAS_ROAMING
// FLAG_HIDE_NUVEMA_TOWN_PLAYERS_HOUSE_MOM

// removed running shoes manual as metatile

// For future use:
//NuvemaTown_PlayersHouse_1F_EventScript_SetMovingBoxes::
//	setmetatile 5, 4, METATILE_PlayersHouse_MovingBox_Open, TRUE
//	setmetatile 5, 2, METATILE_PlayersHouse_MovingBox_Closed, TRUE
//	return


// special TurnOnTVScreen
// special TurnOffTVScreen
// special InitRoamer


// Useful for later
// EventScript_PlayersHouseLatiNewsFlash::
// 	msgbox PlayersHouse_1F_Text_LatiEmergencyNewsFlash, MSGBOX_DEFAULT
// 	special InitRoamer
// 	clearflag FLAG_SYS_TV_LATIAS_LATIOS
// 	setflag FLAG_LATIOS_OR_LATIAS_ROAMING
// 	special TurnOffTVScreen
// 	releaseall
// 	end

script NuvemaTown_PlayersHouse_2F_EventScript_IntroCutscene {
	lockall
	applymovement(LOCALID_CHEREN, Cheren_Approaches)
	waitmovement(0)
    msgbox(Cheren_Intro, MSGBOX_NPC)
	applymovement(LOCALID_CHEREN, Cheren_Sees_Starters)
	waitmovement(0)
	msgbox(Cheren_Waiting, MSGBOX_NPC)
	applymovement(LOCALID_CHEREN, Cheren_Looks_at_Player)
	waitmovement(0)
	msgbox(Cheren_Time, MSGBOX_NPC)
	call(PlayersHouse_2F_EventScript_SetWallClock)
	setvar(VAR_NUVEMA_TOWN_STATE,2)
    addobject(LOCALID_BIANCA)
	applymovement(LOCALID_BIANCA, Bianca_Climbs_Stairs)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Player_Notices_Bianca)
	waitmovement(0)
	applymovement(LOCALID_CHEREN, Cheren_Notices_Bianca)
	waitmovement(0)
    msgbox(Bianca_Arrives, MSGBOX_NPC)
	applymovement(LOCALID_CHEREN, Cheren_Meets_Bianca)
	waitmovement(0)
    msgbox(Cheren_Berating, MSGBOX_NPC)
	applymovement(LOCALID_BIANCA, Bianca_Enters)
	waitmovement(0)
	applymovement(LOCALID_CHEREN, Cheren_Faces_Group)
	applymovement(OBJ_EVENT_ID_PLAYER, Player_Faces_Group)
	waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(Bianca_Apology, MSGBOX_NPC)
	applymovement(LOCALID_BIANCA, Bianca_Approaches_Starters)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Player_Faces_Bianca)
	waitmovement(0)
    msgbox(Bianca_TimeToPick, MSGBOX_NPC)
	msgbox(Cheren_Response, MSGBOX_NPC)
	applymovement(LOCALID_CHEREN, Cheren_Approaches_Starters)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Player_Faces_Starters)
	waitmovement(0)
	applymovement(LOCALID_BIANCA, Bianca_Faces_Starters)
	waitmovement(0)
    msgbox(Cheren_TimeToPick, MSGBOX_NPC)
	releaseall
}

script NuvemaTown_PlayersHouse_2F_EventScript_StarterBag {
	lockall
	setflag(FLAG_SYS_POKEMON_GET)
	fadescreen(FADE_TO_BLACK)
	special(ChooseStarter)
	setflag(FLAG_SYS_POKEDEX_GET)
	special(SetUnlockedPokedexFlags)
	setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
	releaseall
}

script PlayersHouse_2F_EventScript_CheckWallClock {
	incrementgamestat(GAME_STAT_CHECKED_CLOCK)
	fadescreen(FADE_TO_BLACK)
	special(Special_ViewWallClock)
	waitstate
	releaseall
	end
}


script PlayersHouse_2F_EventScript_SetWallClock {
	fadescreen(FADE_TO_BLACK)
	special(StartWallClock)
	waitstate
	setflag(FLAG_SET_WALL_CLOCK)
	return
}

//Intro Script
movement Cheren_Approaches {
    walk_down * 1
	face_left
}

text Cheren_Intro{
	format("Cheren: {PLAYER}! I heard from Professor Juniper. We can have a Pokémon?")
}

movement Cheren_Sees_Starters {
	face_down
}

text Cheren_Waiting{
	format("...\l...\nWhat's keeping Bianca?")
}

movement Cheren_Looks_at_Player {
	face_left
}

movement Player_Looks_at_Cheren {
	face_right
}

text Cheren_Time{
	format("What time is it anyways?")
}

movement Bianca_Climbs_Stairs{
    walk_down * 1
}

movement Player_Notices_Bianca {
	face_up
}

movement Cheren_Notices_Bianca {
	face_up
}

text Bianca_Arrives{
    format("Bianca: Am I a little late again? So-oooo-orry!")
}

movement Cheren_Meets_Bianca{
    walk_right * 1
	walk_up * 1
}

text Cheren_Berating{
    format("Cheren: Bianca...\nI've known for ten years that you have no sense of time, but...seriously...\NToday's the day we can get a Pokémon from Professor Juniper!")
}

movement Bianca_Enters{
    walk_down * 1
	walk_left * 4
	walk_down * 1
}

movement Cheren_Faces_Group{
    face_left
}

movement Player_Faces_Group{
	face_up
}

text Bianca_Apology{
    format("Bianca: I know. Sorry, {PLAYER}. Sorry, Cheren.")
}

movement Bianca_Approaches_Starters{
	walk_left * 1
	walk_down * 2
	face_player
}

movement Player_Faces_Bianca{
	face_left
}

text Bianca_TimeToPick{
    format("Bianca: So, where are the Pokémon?\nThey were delivered to {PLAYER}'s house, so {PLAYER} gets first pick.")
}

text Cheren_Response{
    format("Cheren: Naturally.")
}

movement Cheren_Approaches_Starters{
	walk_down * 1
	walk_left * 1
	face_down
}

movement Player_Faces_Starters{
	face_down
}

movement Bianca_Faces_Starters{
	face_down
}

text Cheren_TimeToPick{
    format("The Pokémon are waiting for us inside that gift box.\nOk, {PLAYER}, you go first and take a peek at the gift box.\nI want to meet the Pokémon right now!")
}